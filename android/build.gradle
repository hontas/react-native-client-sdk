buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion = 31
    buildToolsVersion = "30.0.3"

    defaultConfig {
        minSdkVersion(21)
        targetSdkVersion(31)
        versionCode = 1
        versionName = "1.0"
        consumerProguardFiles("consumer-proguard-rules.pro")
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def resolvedNodeModulesDir = null
try {
    resolvedNodeModulesDir = new File(["node", "--print", "require.resolve('react-native')"].execute(null, rootDir).text.trim()).getParentFile().getParentFile();
} catch (Throwable ignored) {}

def modulesPath = resolvedNodeModulesDir
    ? resolvedNodeModulesDir
    : "$rootDir/../node_modules"

allprojects {
    repositories {
        // Ripped from https://stackoverflow.com/questions/74333787/react-native-2-files-found-with-path-lib-arm64-v8a-libfbjni-so-from-inputs
        exclusiveContent {
            // We get React Native's Android binaries exclusively through npm,
            // from a local Maven repo inside node_modules/react-native/.
            // (The use of exclusiveContent prevents looking elsewhere like Maven Central
            // and potentially getting a wrong version.)
            filter {
                includeGroup "com.facebook.react"
            }
            forRepository {
                maven {
                    url "$modulesPath/react-native/android"
                }
            }
        }

        mavenLocal()
        google()
        mavenCentral()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$modulesPath/react-native/android"
        }
    }
}

dependencies {
    implementation("com.facebook.react:react-native:+")
    implementation("com.launchdarkly:launchdarkly-android-client-sdk:3.6.0")
    implementation("com.jakewharton.timber:timber:5.0.1")
    implementation("com.google.code.gson:gson:2.8.9")
}

